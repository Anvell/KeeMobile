import constants.BuildVersions
import constants.Dependencies

buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath Dependencies.Core.androidGradlePlugin
        classpath Dependencies.Kotlin.gradlePlugin
        classpath Dependencies.Kotlin.serialization
        classpath Dependencies.Hilt.gradlePlugin
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.38.0"
    id "com.diffplug.spotless" version "5.12.5"
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion BuildVersions.compileSdk
                buildToolsVersion BuildVersions.buildTools

                defaultConfig {
                    minSdkVersion BuildVersions.minSdk
                    targetSdkVersion BuildVersions.targetSdk
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                if (android.hasProperty('kotlinOptions')) {
                    kotlinOptions {
                        jvmTarget = "1.8"
                        useIR = true

                        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
                    }
                }
            }
        }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint("0.41.0") userData 'disabled_rules': 'no-wildcard-imports'
        }
    }
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        it.candidate.version.toLowerCase().contains('-alpha')
    }
}
