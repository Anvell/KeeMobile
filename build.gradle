import constants.BuildVersions
import constants.Dependencies

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Dependencies.Core.androidGradlePlugin
        classpath Dependencies.Kotlin.gradlePlugin
        classpath Dependencies.Kotlin.androidExtensions
        classpath Dependencies.Hilt.gradlePlugin
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.29.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            android {
                compileSdkVersion BuildVersions.compileSdk
                buildToolsVersion BuildVersions.buildTools

                defaultConfig {
                    minSdkVersion BuildVersions.minSdk
                    targetSdkVersion BuildVersions.targetSdk
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                if (android.hasProperty('kotlinOptions')) {
                    kotlinOptions {
                        jvmTarget = "1.8"
                        freeCompilerArgs += [
                                "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                                "-Xopt-in=kotlinx.coroutines.FlowPreview",
                                "-Xopt-in=kotlin.Experimental"
                        ]
                    }
                }
            }
        }

        // Turn off kotlin synthetics for all submodules
        if (project.hasProperty('androidExtensions')) {
            androidExtensions {
                features = ["parcelize"]
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
